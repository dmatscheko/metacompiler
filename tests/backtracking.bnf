"EBNF of EBNF (can NOT parse)" {

EBNF        = [ Title ] "{" { Production } "}" name [ Comment ] ;
Production  = name "=" [ Expression ] ";" ;
Expression  = name | ( token [ "..." token ] ) | Group | Option | Repetition | skipspaces | Sequence | Alternative ;
Sequence    = Expression Expression { Expression } ;
Alternative = Expression "|" Expression { "|" Expression } ;
Group       = "(" Expression ")" ;
Option      = "[" Expression "]" ;
Repetition  = "{" Expression "}" ;
Title       = token ;
Comment     = token ;

name        = ( Small | Caps ) - { Small | Caps | Digit | "_" } + ;
token       = Dquotetoken | Squotetoken ;

Dquotetoken = '"' - { Small | Caps | Digit | Special | "~" | "'" | '\\"' } '"' + ;
Squotetoken = "'" - { Small | Caps | Digit | Special | "~" | '"' | "\\'" } "'" + ;

Digit       = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
Small       = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" |
              "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;
Caps        = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" |
              "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" ;
Special     = "_" | " " | "." | "," | ":" | ";" | "!" | "?" | "+" | "-" | "*" | "/" | "=" |
              "(" | ")" | "{" | "}" | "[" | "]" | "<" | ">" | "|" | "%" | "$" | "&" | "#" |
              "@" | "\\\\" | "\\t" | "\t" | "\\n" | "\n" | "\\r" | "\r" ;

skipspaces  = Skip | Noskip ;
Skip        = "+" ;
Noskip      = "-" ;

}
EBNF