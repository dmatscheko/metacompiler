:title("EBNF of ABNF") ;
:startRule(ABNF) ;


// This is a parser command that sets the possible white space.
:skip(Whitespace) ;

// This is the start rule.
ABNF        = { Production | LineCommand } ;

Production  = Name [ Tag ] "=" [ Expression ] ";" ;

Expression  = Alternative ;

Alternative = Sequence { "|" Sequence } ;

Sequence    = Term { Term } ;

Term        = TaggedTerm | Command ;

TaggedTerm  = ( Name | ByteRange | Range | CharsOf | CharOf | Group | Option | Repetition | Times ) [ Tag ] ;

ByteRange   = Token "..b" Token ;
Range       = Token [ "..." Token ] ;
CharsOf     = "@" Token "+" ;
CharOf      = "@" Token ;
Group       = "(" Expression ")" ;
Option      = "[" Expression "]" ;
Repetition  = "{" Expression "}" ;
Times       = Number [ "..." ( Number | "" ) ] Group ;

Tag         = "<" ( Name | Token ) { "," ( Name | Token ) } ">" ;

LineCommand = Command ";" ;

Command     = ":" CmdName "(" [ ( Name | Token | Number ) { "," ( Name | Token | Number ) } ] ")" ;
CmdName     = Alphabet :skip() { Alphabet | Digit | "_" } :skip(Whitespace) ;

Name        = Alphabet :skip() { Alphabet | Digit | "_" } :skip(Whitespace) ;

Token       = Dquotetoken | Squotetoken | Code ;
Dquotetoken = '"' :skip() { AsciiNoQs | "'" | '\\"' } '"' :skip(Whitespace) ;
Squotetoken = "'" :skip() { AsciiNoQs | '"' | "\\'" } "'" :skip(Whitespace) ;
Code        = '~~' :skip() { [ "~" ] AllButTilde } '~~' :skip(Whitespace) ;

Alphabet    = "a"..."z" | "A"..."Z" ;
Digit       = "0"..."9" ;
AsciiNoQs   = "\x28"..."\x7E" | "\x23"..."\x26" | "\t" | "\n" | "\r" | " " | "!" ; // Readable ASCII without double and single quotes.
AsciiNoLb   = "\x20"..."\x7E" | "\t" ; // Readable ASCII without line breaks (CR and LF).
AsciiNoStSl = "\x00"...")" | "+"..."." | "0"..."~" ; // All ASCII without star (*) and slash (/).
AllButTilde = "\x00"..."\x7D" | "\\~" | "\x7f"..."\uffff" ; // All ASCII and unicode chars. Only tilde is escaped.

Number      = "0" | "1"..."9" { "0"..."9" } ;

Whitespace  = { @"\t\n\r "+ | Comment } ;

Comment     = LineComment | "/*" :skip() { { "*" } AsciiNoStSl { "/" } } "*/" :skip(Whitespace) ;
LineComment = "//" :skip() { AsciiNoLb } ( "\n" | "\r" ) :skip(Whitespace) ;
