:title("EBNF of ABNF") ;
:startRule(ABNF) ;


# This is a parser command that sets the possible white space.
:skip(Whitespace) ;

# This is the start rule.
ABNF        = { Production | LineCommand | Comment } ;

Production  = Name [ Tag ] "=" [ Expression ] ";" ;

Expression  = Alternative ;

Alternative = Sequence { "|" Sequence } ;

Sequence    = Term { Term } ;

Term        = TaggedTerm | Command ;

TaggedTerm  = ( Name | Range | Group | Option | Repetition | Times ) [ Tag ] ;

Range       = Token [ "..." Token ] ;
ByteRange   = Token "..b" Token ;
Group       = "(" Expression ")" ;
Option      = "[" Expression "]" ;
Repetition  = "{" Expression "}" ;
Times       = Number [ "..." ( Number | "" ) ] Group ;

Comment     = "#" :skip(Nothing) { AsciiNoLb } ( "\n" | "\r" ) :skip(Whitespace) ;

Tag         = "<" ( Name | Token ) { "," ( Name | Token ) } ">" ;

LineCommand = Command ";" ;

Command     = ":" CmdName "(" ( Name | Token ) { "," ( Name | Token ) } ")" ;
CmdName     = Alphabet :skip(Nothing) { Alphabet | Digit | "_" } :skip(Whitespace) ;

Name        = Alphabet :skip(Nothing) { Alphabet | Digit | "_" } :skip(Whitespace) ;

Token       = Dquotetoken | Squotetoken | Code ;
Dquotetoken = '"' :skip(Nothing) { AsciiNoQs | "'" | '\\"' } '"' :skip(Whitespace) ;
Squotetoken = "'" :skip(Nothing) { AsciiNoQs | '"' | "\\'" } "'" :skip(Whitespace) ;
Code        = '~~' :skip(Nothing) { [ "~" ] AllButTilde } '~~' :skip(Whitespace) ;

Alphabet    = "a"..."z" | "A"..."Z" ;
Digit       = "0"..."9" ;
AsciiNoQs   = "\x28"..."\x7E" | "\x23"..."\x26" | "\t" | "\n" | "\r" | " " | "!" ; # Readable ASCII without double and single quotes
AsciiNoLb   = "\x20"..."\x7E" | "\t" ; # Readable ASCII without line breaks (CR and LF)
AllButTilde = "\x00"..."\x7D" | "\\~" | "\x7f"..."\uffff" ; # All ASCII and unicode chars. Only tilde is escaped

Number      = "0" | "1"..."9" { "0"..."9" } ;

Nothing     = "" ;
Whitespace  = " \t\r\n" ;
