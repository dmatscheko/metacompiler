"a-EBNF of a-EBNF to a-grammar"

<~~
var names = [];
function getNameIdx(name) {
    var pos = names.indexOf(name);
    if (pos != -1) { return pos };
    return names.push(name)-1;
}

function buildProduction(prodName, prodTag, prodExpression) {
    if (prodTag != undefined) {
        var tag = ebnf.newTag(prodTag.TagChilds, simplifyToArr(prodExpression), prodTag.Pos);
        return ebnf.newProduction(prodName.String, prodName.Int, [tag], prodName.Pos)
    } else {
        return ebnf.newProduction(prodName.String, prodName.Int, [prodExpression], prodName.Pos)
    }
}

function simplifyToArr(rule) {
    if (rule.Operator == ebnf.oid.Sequence) {
        return rule.Childs
    }
    if (rule.Operator == ebnf.oid.Or && rule.Childs.length <= 1) {
        return rule.Childs
    }
    return [ rule ]
}

function simplify(rule) {
    if (rule.Childs.length == 1) {
        return rule.Childs[0]
    }
    if (rule.Childs.length == 0) {
        return undefined
    }
    return rule
}

c.compile(c.asg)
~~>
{

AEBNF       <~~ print("   => Productions: "+ebnf.sprintProductions(ltr.stack)+"\n\n") ~~>
            = [ Title <~~ pushg(pop()) ~~> ] [ Prolog <~~ pushg(pop()) ~~> ] "{" <~~ pushg([]) ~~> { Production <~~ pushg(append(popg(), pop())) //3 ~~> | Comment } "}" <~~ pushg(ebnf.newSequence(popg(), c.Pos)) ~~> [ Epilog <~~ pushg(pop()) ~~> ] [ Start <~~ pushg(pop()) ~~> ] [ Description <~~ pushg(pop()) ~~> ] ;

Production  <~~ push(popg()) ~~>
            = Name <~~ pushg(pop()) ~~> [ Tag <~~ ltr.prodTag=pop() ~~> ] "=" [ Expression <~~ pushg(buildProduction(popg(), ltr.prodTag, pop())); ltr.prodTag=undefined ~~> ] ";" ;

Expression  <~~ push(simplify(ebnf.newAlternative(popg(), c.Pos))) ~~>
            = Alternative <~~ pushg([pop()]) ~~> { "|" Alternative <~~ pushg(append(popg(), pop())) //2 ~~> } ;

Alternative <~~ push(simplify(ebnf.newSequence(popg(), c.Pos))) ~~>
            = Term <~~ pushg([pop()]) ~~> { Term <~~ pushg(append(popg(), pop())) //1 ~~> } ;

Term        = Range | TaggedTerm ;

TaggedTerm <~~ push(popg()) ~~>
            = ( Name | Group | Option | Repetition | Skipspaces ) <~~ pushg(pop()) ~~> [ Tag <~~ var tag=pop(); pushg(ebnf.newTag(tag.TagChilds, [popg()], tag.Pos)) ~~> ] ;

Range       <~~ push(popg()) ~~>
            = TaggedToken <~~ pushg(pop()) ~~> [ "..." TaggedToken <~~ pushg(ebnf.newRange([popg(), pop()], c.Pos)) ~~> ] ;
Group       = "(" Expression <~~ push(ebnf.newGroup(simplifyToArr(pop()), c.Pos)) ~~> ")" ;
Option      = "[" Expression <~~ push(ebnf.newOption(simplifyToArr(pop()), c.Pos)) ~~> "]" ;
Repetition  = "{" Expression <~~ push(ebnf.newRepetition(simplifyToArr(pop()), c.Pos)) ~~> "}" ;
Skipspaces  = Skip | Noskip ;

TaggedToken <~~ push(popg()) ~~>
            = Token <~~ pushg(pop()) ~~> [ Tag <~~ var tag=pop(); pushg(ebnf.newTag(tag.TagChilds, [popg()], tag.Pos)) ~~> ] ;

Start       = Name ;
Title       = Token ;
Description = Token ;
Comment     = "#" - { Small | Caps | Digit | Special | "~" | '"' | "'" } ( "\n" | "\r" ) + ;

Prolog      = Tag ;
Epilog      = Tag ;

Tag         <~~ push(ebnf.newTag(popg(), undefined, c.Pos)) ~~>
            = "<" Code <~~ pushg([pop()]) ~~> { "," Code <~~ pushg(append(popg(), pop())) ~~> } ">" ;

Code        = '~~' - { [ "~" ] Codeinner } <~~ push(ebnf.newToken(up.in, c.Pos)) ~~> '~~' + ;
Codeinner   = Small | Caps | Digit | Special | "'" | '"' | "\\~" | "\n" | "\r" ;

Name        <~~ push(ebnf.newName(up.in, getNameIdx(up.in), c.Pos)) ~~>
            = ( Small | Caps ) - { Small | Caps | Digit | "_" } + ;
Token       = ( Dquotetoken | Squotetoken ) ;

Dquotetoken = '"' - { Small | Caps | Digit | Special | "~" | "'" | '\\"' | "\n" | "\r" } <~~ push(ebnf.newToken(up.in, c.Pos)) ~~> '"' + ;
Squotetoken = "'" - { Small | Caps | Digit | Special | "~" | '"' | "\\'" | "\n" | "\r" } <~~ push(ebnf.newToken(up.in, c.Pos)) ~~> "'" + ;

Digit       = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
Small       = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" |
              "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;
Caps        = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" |
              "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" ;
Special     = "_" | " " | "." | "," | ":" | ";" | "!" | "?" | "+" | "-" | "*" | "/" | "=" |
              "(" | ")" | "{" | "}" | "[" | "]" | "<" | ">" | "|" | "%" | "$" | "&" | "#" |
              "@" | "\\" | "\t" ;

Skip        = "+" <~~ push(ebnf.newSkipSpace(true, c.Pos)) ~~> ;
Noskip      = "-" <~~ push(ebnf.newSkipSpace(false, c.Pos)) ~~> ;

}
AEBNF