"a-EBNF of a-EBNF"

<~~
var names = [];
function getNameIdx(name) {
    var pos = names.indexOf(name);
    if (pos != -1) { return pos };
    return names.push(name)-1;
}
c.compile(c.asg)
~~>
{

AEBNF       = [ Title ] [ Prolog ] "{" { Production | Comment } "}" [ Epilog ] [ Start ] [ Description ] ;
Production  <~~ push(popg());   var foo = pop();  print("   => Productions: "+ebnf.sprintProductions([foo])+"\n\n"); // exit(0) ~~>
            = Name <~~ pushg(pop()) ~~> [ Tag <~~ ltr.pTag=pop() ~~> ] "=" [ Expression <~~ var name=popg(); if (ltr.pTag != undefined) { var tag = ebnf.newTag(ltr.pTag.TagChilds, [pop()]); pushg(ebnf.newProduction(name.String, name.Int, [tag])) } else { pushg(ebnf.newProduction(name.String, name.Int, [pop()])) } ~~> ] ";" ;

Expression  <~~ var a=popg(); if (a.legth == 1) { push(a[0]) } else { push(ebnf.newAlternative(a)) } ~~>
            = Alternative <~~ pushg([pop()]) ~~> { "|" Alternative <~~ pushg(append( popg(), pop() )) ~~> } ;
Alternative <~~ var a=popg(); if (a.legth == 1) { push(a[0]) } else { push(ebnf.newSequence(a)) } ~~>
            = Term <~~ pushg([pop()]) ~~> { Term <~~ pushg(append( popg(), pop() )) ~~> } ;

Term        = Range | ( Name | Group | Option | Repetition | Skipspaces ) [ Tag ] ;

Range       = Token [ "..." Token ] ;
Group       = "(" Expression <~~ push(ebnf.newGroup([pop()])) ~~> ")" ;
Option      = "[" Expression <~~ push(ebnf.newOption([pop()])) ~~> "]" ;
Repetition  = "{" Expression <~~ push(ebnf.newRepetition([pop()])) ~~> "}" ;
Skipspaces  = Skip | Noskip ;

Start       = Name ;
Title       = Token ;
Description = Token ;
Comment     = "#" - { Small | Caps | Digit | Special | "~" | '"' | "'" } ( "\n" | "\r" ) + ;

Prolog      = Tag ;
Epilog      = Tag ;

Tag         <~~ push(ebnf.newTag(popg())) ~~>
            = "<" Code <~~ pushg([pop()]) ~~> { "," Code <~~ pushg(append( popg(), pop() )) ~~> } ">" ;

Code        = '~~' - { [ "~" ] Codeinner } <~~ push(ebnf.newToken(up.in)) ~~> '~~' + ;
Codeinner   = Small | Caps | Digit | Special | "'" | '"' | "\\~" | "\n" | "\r" ;

Name        <~~ push(ebnf.newName(up.in, getNameIdx(up.in))) ~~>
            = ( Small | Caps ) - { Small | Caps | Digit | "_" } + ;
Token       = ( Dquotetoken | Squotetoken ) [ Tag ] ;

Dquotetoken = '"' - { Small | Caps | Digit | Special | "~" | "'" | '\\"' | "\n" | "\r" } <~~ push(ebnf.newToken(up.in)) ~~> '"' + ;
Squotetoken = "'" - { Small | Caps | Digit | Special | "~" | '"' | "\\'" | "\n" | "\r" } <~~ push(ebnf.newToken(up.in)) ~~> "'" + ;

Digit       = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
Small       = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" |
              "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;
Caps        = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" |
              "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" ;
Special     = "_" | " " | "." | "," | ":" | ";" | "!" | "?" | "+" | "-" | "*" | "/" | "=" |
              "(" | ")" | "{" | "}" | "[" | "]" | "<" | ">" | "|" | "%" | "$" | "&" | "#" |
              "@" | "\\" | "\t" ;

Skip        = "+" <~~ push(ebnf.newSkipSpace(true)) ~~> ;
Noskip      = "-" <~~ push(ebnf.newSkipSpace(false)) ~~> ;

}
AEBNF