:title("EBNF of EBNF") ;
:startRule(EBNF) ;


// This is a parser command that sets the possible white space.
:whitespace(Whitespace) ;

// This is the start rule.
EBNF        = { Production | LineCommand } ;

Production  = Name "=" [ Expression ] ";" ;

Expression  = Alternative ;
Alternative = Sequence { "|" Sequence } ;
Sequence    = Term { Term } ;

Term        = Name | ByteRange | Range | CharsOf | CharOf | Group | Option | Repetition | Times | Command ;

ByteRange   = Token "..b" Token ;
Range       = Token [ "..." Token ] ;
CharsOf     = "@" Token "+" ;
CharOf      = "@" Token ;
Group       = "(" Expression ")" ;
Option      = "[" Expression "]" ;
Repetition  = "{" Expression "}" ;
Times       = Number [ "..." ( Number | "" ) ] Group ;

LineCommand = Command ";" ;

Command     = ":" CmdName "(" [ ( Name | Token | Number ) { "," ( Name | Token | Number ) } ] ")" ;
CmdName     = Alphabet :whitespace() { Alphabet | Digit | "_" } :whitespace(Whitespace) ;

Name        = Alphabet :whitespace() { Alphabet | Digit | "_" } :whitespace(Whitespace) ;

Token       = Dquotetoken | Squotetoken ;
Dquotetoken = '"' :whitespace() { AsciiNoQs | "'" | '\\"' } '"' :whitespace(Whitespace) ;
Squotetoken = "'" :whitespace() { AsciiNoQs | '"' | "\\'" } "'" :whitespace(Whitespace) ;

Alphabet    = "a"..."z" | "A"..."Z" ;
Digit       = "0"..."9" ;
AsciiNoQs   = "\x28"..."\x7E" | "\x23"..."\x26" | "\t" | "\n" | "\r" | " " | "!" ; // Readable ASCII without double and single quotes.
AsciiNoLb   = "\x20"..."\x7E" | "\t" ; // Readable ASCII without line breaks (CR and LF).
AsciiNoStSl = "\x00"...")" | "+"..."." | "0"..."~" ; // All ASCII without star (*) and slash (/).

Number      = "0" | "1"..."9" { "0"..."9" } ;

Whitespace  = { @"\t\n\r "+ | Comment } ;

Comment     = LineComment | "/*" :whitespace() { { "*" } AsciiNoStSl { "/" } } "*/" :whitespace(Whitespace) ;
LineComment = "//" :whitespace() { AsciiNoLb } ( "\n" | "\r" ) :whitespace(Whitespace) ;
