"EBNF of EBNF" {

EBNF        = [ Title ] "{" { Production | Comment } "}" [ Start ] [ Description ] ;
Production  = Name "=" [ Expression ] ";" ;


Expression  = Alternative { "|" Alternative } ;
Alternative = Term { Term } ;

Term        = Name | Range | Group | Option | Repetition | Skipspaces ;

Range       = Token [ "..." Token ] ;
Group       = "(" Expression ")" ;
Option      = "[" Expression "]" ;
Repetition  = "{" Expression "}" ;
Skipspaces  = Skip | Noskip ;

Title       = Token ;
Description = Token ;
Start       = Name ;
Comment     = "#" - { Small | Caps | Digit | Special | "~" | '"' | "'" } ( "\n" | "\r" ) + ;

Name        = ( Small | Caps ) - { Small | Caps | Digit | "_" } + ;
Token       = Dquotetoken | Squotetoken ;

Dquotetoken = '"' - { Small | Caps | Digit | Special | "~" | "'" | '\\"' | "\n" | "\r" } '"' + ;
Squotetoken = "'" - { Small | Caps | Digit | Special | "~" | '"' | "\\'" | "\n" | "\r" } "'" + ;

Digit       = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
Small       = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" |
              "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" ;
Caps        = "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" |
              "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" ;
Special     = "_" | " " | "." | "," | ":" | ";" | "!" | "?" | "+" | "-" | "*" | "/" | "=" |
              "(" | ")" | "{" | "}" | "[" | "]" | "<" | ">" | "|" | "%" | "$" | "&" | "#" |
              "@" | "\\" | "\t" ;

Skip        = "+" ;
Noskip      = "-" ;

}
EBNF