"EBNF of EBNF"
{

# This is a parser command that sets the possible white space.
:skip(Whitespace) ;

# This is the start rule.
EBNF        = [ Title ] "{" { Production | LineCommand | Comment } "}" [ Start ] [ Description ] ;

Production  = Name "=" [ Expression ] ";" ;

Expression  = Alternative ;
Alternative = Sequence { "|" Sequence } ;
Sequence    = Term { Term } ;

Term  = Name | Range | Group | Option | Repetition | Times | Command ;

Range       = Token [ "..." Token ] ;
Group       = "(" Expression ")" ;
Option      = "[" Expression "]" ;
Repetition  = "{" Expression "}" ;
Times       = Number [ "..." ( Number | "" ) ] Group ;

Start       = Name ;
Title       = Token ;
Description = Token ;
Comment     = "#" :skip(Nothing) { AsciiNoLb } ( "\n" | "\r" ) :skip(Whitespace) ;

LineCommand = Command ";" ;

Command     = ":" CmdName "(" ( Name | Token ) { "," ( Name | Token ) } ")" ;
CmdName     = Alphabet :skip(Nothing) { Alphabet | Digit | "_" } :skip(Whitespace) ;

Name        = Alphabet :skip(Nothing) { Alphabet | Digit | "_" } :skip(Whitespace) ;

Token       = Dquotetoken | Squotetoken ;
Dquotetoken = '"' :skip(Nothing) { AsciiNoQs | "'" | '\\"' } '"' :skip(Whitespace) ;
Squotetoken = "'" :skip(Nothing) { AsciiNoQs | '"' | "\\'" } "'" :skip(Whitespace) ;

Alphabet    = "a"..."z" | "A"..."Z" ;
Digit       = "0"..."9" ;
AsciiNoQs   = "\x28"..."\x7E" | "\x23"..."\x26" | "\t" | "\n" | "\r" | " " | "!" ; # Readable ASCII without double and single quotes
AsciiNoLb   = "\x20"..."\x7E" | "\t" ; # Readable ASCII without line breaks (CR and LF)

Number      = "0" | "1"..."9" { "0"..."9" } ;

Nothing     = "" ;
Whitespace  = " \t\r\n" ;

}
EBNF
