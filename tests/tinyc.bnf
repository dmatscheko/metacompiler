:title("Tiny C-ish language") ;


:startRule(Program) ;
:whitespace(Whitespace) ;

Program     = { Statement } ;
Statement   = IfElse | If | For | While | Do | Block | SingleStmt | ZeroStmt ;

IfElse      = "if" ParenExpr Statement "else" Statement ;
If          = "if" ParenExpr Statement ;
For         = "for" ParenExpr Statement ;
While       = "while" ParenExpr Statement ;
Do          = "do" Statement "while" ParenExpr ";" ;
Block       = "{" { Statement } "}" ;
SingleStmt  = Expression ";" ;
ZeroStmt    = ";" ;

ParenExpr   =  "(" Expression { ";" Expression } ")" ;

Expression  = VoidExpr | ResultExpr ;
VoidExpr    = Declaration | Assignment ;
ResultExpr  = Call | Test ;

Declaration = "var" ( Assignment | Id ) ;

Assignment  = Id "=" ResultExpr ;

Call        = Id "(" [ ResultExpr { "," ResultExpr } ] ")" ;

Test        = Comp ( "||" | "&&" ) Comp | Comp ;
Comp        = Sum ( "<" | ">" | "==" | "!=" ) Sum | Sum ;
Sum         = Term "+" Term | Term "-" Term | Id "++" | Id "--" | Term ;
Term        = ParenExpr | Call | Id | Int ;

Id          = ( "a"..."z" | "A"..."Z" ) :whitespace() { "a"..."z" | "A"..."Z" | "_" } :whitespace(Whitespace) ;
Int         = "0" | "1"..."9" :whitespace() { "0"..."9" } :whitespace(Whitespace) ;

Whitespace  = @+" \t\r\n" ;

// The first option(s) of an OR must not be the beginning of a later option. Otherwise only the first option would be found and success would be returned.
// No option of a production can be equal to the production itself. This means that e.g. the production 'Test' must not have an option 'Test'.


:startScript(~~ c.compile(c.asg) ~~) ;