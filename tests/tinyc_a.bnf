"Tiny-C language (plus a few small enhancements)"

<~~ c.compile(c.asg) ~~>
{

Program     = { Statement } ;
Statement   = "if" ParenExpr Statement
            | "if" ParenExpr Statement "else" Statement
            | "while" ParenExpr Statement
            | "do" Statement "while" ParenExpr ";"
            | "{" { Statement } "}"
            | Expr ";"
            | ";" ;

ParenExpr   =  "(" Expr ")" ;

Expr        = Assignment | Command | Test ;

Test        = Comp ( "||" | "&&" ) Comp | Comp ;
Comp        = Sum ( "<" | ">" | "==" | "!=" ) Sum | Sum ;
Sum         = Sum "+" Term | Sum "-" Term | Term ;
Term        = ParenExpr | Id | Int ;

Assignment  = Id "=" Expr ;

Command     = Id "(" [ Parameter { "," Parameter } ] ")" ;
Parameter   = Command | Test ;

Id          = ( "a"..."z" | "A"..."Z" ) { "a"..."z" | "A"..."Z" | "_" } ;
Int         = "0" | "1"..."9" { "0"..."9" } ;

}
Program
